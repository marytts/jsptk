plugins {
    id 'c'
    id "java"
    id 'maven-publish'
    id "net.tribe-seven.swig" version "0.1.1"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
    maven { url 'https://oss.jfrog.org/artifactory/oss-snapshot-local' }
}

dependencies {
    compile group: 'org.ejml', name: 'ejml-all', version: '0.34'
    testCompile group: 'org.assertj', name:'assertj-core', version: '3.10.0'
    testCompile group: 'org.testng', name: 'testng', version: '6.8.13'
    testCompile group: 'com.google.guava', name: 'guava', version: '18.0'
}

test {
    useTestNG()
}

task generateSrc(type: SwigTask) {
    description "Generate the JNI wrapping necessary files"

    // FIXME
    verbose = true
    // enableCpp = true

    // Pathes
    javaSourcesPath = file("$buildDir/generatedSrc/main/java/jsptk")
    source = new File("$projectDir/swig/sptk.i")
    wrapperTargetFile = file("$buildDir/generatedSrc/main/c/sptk_wrap.c")
    ext {
        sourceDir = layout.buildDirectory.dir('generatedSrc/main/c')
        headerDir = layout.projectDirectory.dir('SPTK/include')
    }

    // Java info
    module = "Sptk"
    packageName = "jsptk"
}


sourceSets {
    main {
        java {
            srcDirs += generateSrc.javaSourcesPath
        }
    }
}

compileJava {
    mustRunAfter generateSrc
}

model {
    components {
        sptk(NativeLibrarySpec) {
            sources {
                generated(CSourceSet) {
                    generatedBy tasks.generateSrc
                }

                c {
                    source {
                        srcDirs 'SPTK/'
                        include '**/*.c'
                        exclude "example/*.c"
                        exclude "wavjoin/*"
                        exclude "wavsplit/*"
                        exclude { details ->
                            details.file.parentFile.parent.endsWith("bin") && details.file.name.endsWith(".c") && (! details.file.name.startsWith("_"))
                        }
                    }
                    exportedHeaders {
                        srcDir 'SPTK/include'
                    }
                }
            }

            binaries {
                all {
                    cCompiler.args '-O3', '-Wall', '-fno-common', '-Wstrict-prototypes'// , '-ansi'
                    cCompiler.args '-I', "${org.gradle.internal.jvm.Jvm.current().javaHome}/include"
                    switch (targetPlatform.operatingSystem) {
                        case { it.macOsX }:
                            cCompiler.args '-I', "${org.gradle.internal.jvm.Jvm.current().javaHome}/include/darwin"
                            break
                        case { it.linux }:
                            cCompiler.args '-I', "${org.gradle.internal.jvm.Jvm.current().javaHome}/include/linux"
                            break
                    }
                }
            }
        }
    }

    tasks {
        processResources {
            dependsOn sptkSharedLibrary
            from "${buildDir}/libs/", {
                include '**/libsptk.*'
                eachFile {
                    it.path = it.name
                }
                includeEmptyDirs = false
            }
        }
    }
}




/**********************************************************************************************
 ** Project meta + publishing part
 ***********************************************************************************************/
version '0.2-SNAPSHOT'
group "de.dfki.mary"
description "Artifact containing a wrapper for SPTK"

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier 'sources'
            }
        }
    }
    repositories {
        maven {
            name 'projectLocal'
            url "$rootProject.buildDir/repo"
        }
        maven {
            url version.endsWith('-SNAPSHOT') ? 'https://oss.jfrog.org/artifactory/oss-snapshot-local' : 'https://api.bintray.com/maven/marytts/marytts/jsptk'
            credentials {
                username = findProperty('bintrayUser')
                password = findProperty('bintrayApiKey')
            }
        }
    }
}
